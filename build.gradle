buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.3"
}
task jacocoMergeReports(type: JacocoMerge) {
    group = 'Reporting'
    description = "Merge all JaCoCo reports from projects into one."

    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            dependsOn "${p.path}:testDebugUnitTest"
            executionData "${p.buildDir}/jacoco/testDebugUnitTest.exec"
        }
    }
}

task jacocoTestReports(type: JacocoReport, dependsOn: 'jacocoMergeReports') {
    group = 'Reporting'
    description = "Generate Jacoco coverage reports for the build. Only unit tests."

    executionData jacocoMergeReports.destinationFile

    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            classDirectories.from(files([
                    fileTree(dir: "${p.buildDir}/tmp/kotlin-classes/debug")
            ]))
            sourceDirectories.from(files([
                    "${p.buildDir}/src/main/java"
            ]))
        }
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false

        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}